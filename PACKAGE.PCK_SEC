CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_SEC" AS 
    --
    REFRESH_TOKEN_EXP CONSTANT PLS_INTEGER:=(24*60*60)*90; --90 DAYS
    --
    ACCESS_TOKEN_EXP CONSTANT PLS_INTEGER:=60*5; -- 5 MINUTES
    --
    TYPE t_JWT_data IS RECORD (
        iss VARCHAR2(10),
        sub VARCHAR2(100),
        aud VARCHAR2(7),
        iat NUMBER,
        exp NUMBER,
        jti NUMBER,
        cld_cloud_name users.cld_cloud_name%type,
        cld_upload_preset users.cld_upload_preset%type,
        client_secret user_ords_clients.client_secret%type
    );
    TYPE t_session_data IS RECORD (
        url VARCHAR2(100),
        timezone VARCHAR2(30),
        canuse_popover BOOLEAN,
        canuse_eyedropper BOOLEAN,
        user_id NUMBER,
        scope VARCHAR2(7),
        iss VARCHAR2(10),
        sub VARCHAR2(100),
        client_secret user_ords_clients.client_secret%type,
        valid BOOLEAN,
        cpu_used_session NUMBER
    );

    g_session_user_id NUMBER;
    g_website_id NUMBER;
    --
    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE getMenulist(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pDomain IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE refreshOrdsEndpoints(pWebsiteId IN website.id%type);
    --
    FUNCTION getSessionData(pWebsiteId IN website.id%type) RETURN t_session_data;
    --
    PROCEDURE updateCpuUsed(pUserid IN users.id%type, pCpuUsedStart IN users.cpu_used%type);
    --
    PROCEDURE post_auth_facebook;
    --
    PROCEDURE post_auth_google;
    --
END PCK_SEC;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_SEC" AS

    /* 
    ** TEMPORARY SOLUTION TO POPULATE ENDPOINT TABLES
    */
    PROCEDURE refreshOrdsEndpoints(pWebsiteId IN website.id%type) IS 
        n pls_integer;
    BEGIN
        DELETE api_endpoint WHERE website_id=pWebsiteId;

        DELETE website_role WHERE website_id=pWebsiteId;
        
        INSERT INTO website_role(website_id, role_id)
        SELECT w.id, r.id
          FROM website w, role r
         WHERE w.id=pWebsiteId
           AND r.name in ('admin','owner');

        INSERT INTO api_endpoint(website_id, role_id, template_id)
        SELECT w.id, r.id, o.template_id 
          FROM website w, role r, ords_endpoints o
         WHERE w.id=pWebsiteId
           AND r.name in ('admin','owner');

        INSERT INTO website_role(website_id, role_id)
        SELECT w.id, r.id
          FROM website w, role r
         WHERE w.id=pWebsiteId
           AND r.name='visitor';

        INSERT INTO api_endpoint(website_id, role_id, template_id)
        SELECT w.id, r.id, o.template_id 
          FROM website w, role r, ords_endpoints o
         WHERE w.id=pWebsiteId
           AND r.name='visitor'
           AND (INSTR(o.uri_template,'message')>0 or INSTR(o.uri_template,'authenticate')>0);

    END;

    /*
    ** RETURN DECODED JWT PAYLOAD AND SIGNING SECRET
    */
    FUNCTION decodeJWT(pToken IN VARCHAR2) RETURN t_JWT_data IS
        l_payload varchar2(500);
        l_decoded varchar2(500);
        pos1 PLS_INTEGER;
        pos2 PLS_INTEGER;
        l_JWT_data t_JWT_data;
    BEGIN
        pos1:=INSTR(pToken,'.',1);
        pos2:=INSTR(pToken,'.',-1);
        l_payload:=SUBSTR(pToken,pos1+1,pos2-pos1-1);
        l_decoded:=utl_raw.cast_to_varchar2(utl_encode.base64_decode(utl_raw.cast_to_raw(l_payload)));

        SELECT j.iss, j.sub, j.aud, j.iat, j.exp, j.jti, j.cld_cloud_name, j.cld_upload_preset, o.client_secret 
          INTO l_JWT_data.iss, l_JWT_data.sub, l_JWT_data.aud, l_JWT_data.iat, l_JWT_data.exp, l_JWT_data.jti, l_JWT_data.cld_cloud_name, l_JWT_data.cld_upload_preset, l_JWT_data.client_secret
          FROM JSON_TABLE(l_decoded, '$' COLUMNS(iss, sub, aud, iat, exp, jti, cld_cloud_name, cld_upload_preset)) j, user_ords_clients o
         WHERE o.name=j.sub;

        RETURN (l_JWT_data);
    END;

    /*
    ** GET SESSION DETAILS OF JWT RESOURCE ACCESS REQUEST
    */
    FUNCTION getSessionData(pWebsiteId IN website.id%type) RETURN t_session_data IS
        l_bearer_token VARCHAR2(1000);
        l_token APEX_JWT.T_TOKEN;
        l_email users.email%type;
        
        l_JWT_data t_JWT_data;
        l_cpu_used_session NUMBER;
        l_session_data t_session_data;
    BEGIN
        SELECT ms.value INTO l_cpu_used_session FROM v$statname sn, v$mystat ms WHERE sn.name='CPU used by this session' AND sn.statistic# = ms.statistic#;

        l_bearer_token := SUBSTR(OWA_UTIL.get_cgi_env('Authorization'), 8);
        l_JWT_data:=decodeJWT(l_bearer_token);
        
        g_session_user_id:=l_JWT_data.jti;
        g_website_id:=pWebsiteId;

        /* Check expiry and signature are ok */
        l_token:=apex_jwt.DECODE(p_value => l_bearer_token, p_signature_key => UTL_RAW.cast_to_raw (l_JWT_data.client_secret));
        BEGIN
            apex_jwt.VALIDATE (p_token => l_token, p_iss => 'ORDS.'||pWebsiteId);
            EXCEPTION WHEN VALUE_ERROR THEN
                pck_core.log('token invalid');
                RAISE_APPLICATION_ERROR(-20000,'token invalid');
        END;

        /* Check that bearer token really is an issued access token */
        IF (l_JWT_data.exp-l_JWT_data.iat>ACCESS_TOKEN_EXP) THEN
            pck_core.log('token not an access token');
            RAISE_APPLICATION_ERROR(-20000,'token not an access token');
        END IF;

        l_session_data.user_id:=l_JWT_data.jti;
        l_session_data.scope:=l_JWT_data.aud;
        l_session_data.iss:=l_JWT_data.iss;
        l_session_data.sub:=l_JWT_data.sub;
        l_session_data.client_secret:=l_JWT_data.client_secret;
        l_session_data.timezone:=OWA_UTIL.get_cgi_env('timezone');
        l_session_data.url:=OWA_UTIL.get_cgi_env('url');
        l_session_data.cpu_used_session:=l_cpu_used_session;

        RETURN l_session_data;
    END;

    /*
    ** USAGE ACCOUNTING BASED ON CPU USED IN SESSION
    */
    PROCEDURE updateCpuUsed(pUserid IN users.id%type, pCpuUsedStart IN users.cpu_used%type) IS
        l_cpu_used_session users.cpu_used%type;
    BEGIN
        SELECT ms.value INTO l_cpu_used_session FROM v$statname sn, v$mystat ms WHERE sn.name='CPU used by this session' AND sn.statistic# = ms.statistic#;
        UPDATE users
           SET cpu_used_last_updated=current_timestamp, 
               cpu_used_created_date=NVL(cpu_used_created_date,current_timestamp),
               cpu_used=NVL(cpu_used,0)+l_cpu_used_session-pCpuUsedStart WHERE id=pUserid;
    END;

    /*
    ** USER SUBMITS EMAIL ADDRESS TO BE AUTHENTICATED
    ** - CREATE RANDOM PASSCODE OR MAGIC LINK AND SEND TO EMAIL ADDRESS
    */
    PROCEDURE createAuthRequest(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_url varchar2(100);
        l_request_type VARCHAR2(10);
        l_user_id users.id%type;
        l_ords_user_id user_ords_clients.id%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_domain website.domain_name%type;
        l_token VARCHAR2(4000);
        l_digits   CONSTANT VARCHAR2(10) := '0123456789';
        l_passcode VARCHAR2(6);
        l_passcode_hash raw(32);
        l_token_hash raw(32);
        l_sendmail_subject VARCHAR2(200);
        l_sendmail_body LONG;
        l_sendmail_link VARCHAR2(500);
        l_return_message VARCHAR2(100);
        
    BEGIN
        SELECT o.client_secret, LOWER(j.email) email, j.url, j.request_type, NVL(u.id,0) user_id, NVL(o.id,0) ords_user_id, j.domain
          INTO l_jwt_enc_key, l_email, l_url, l_request_type, l_user_id, l_ords_user_id, l_domain
          FROM user_ords_clients o, users u, JSON_TABLE(pBodyText, '$' COLUMNS email, url, request_type, domain) j
         WHERE o.name(+)=LOWER(j.email)
           AND u.email(+)=LOWER(j.email);

        /* NEW WEBSITE REQUESTS PUT EMAIL ADDRESS IN THE domain FIELD, WHICH WE CONVERT TO A PROPER SUBDOMAIN NAME */
        IF (l_domain IS NOT NULL) THEN
            l_domain:=pck_hosting.getEditorDomain(l_email);           
            IF (pck_hosting.urlExists(l_domain || '.netlify.app')) THEN
                APEX_JSON.open_object;
                APEX_JSON.write ('success', FALSE);
                APEX_JSON.write ('message', l_domain || '.netlify.app already exists');
                APEX_JSON.close_object;
                pStatus:=200;
                RETURN;
            END IF;
            pck_core.log('Creating website converting email "' || l_email || '" to subdomain "' || l_domain || '"');
        END IF;

        IF (l_ords_user_id=0) THEN
            OAUTH.create_client(
                p_name            => l_email,
                p_grant_type      => 'client_credentials',
                p_owner           => 'mark.russellbrown@gmail.com',
                p_support_email   => 'mark.russellbrown@gmail.com',
                p_description     => 'Provide secure secret in order to encrypt JWT tokens',
                p_privilege_names => 'api_priv'
            );
            SELECT client_secret INTO l_jwt_enc_key FROM user_ords_clients WHERE name=l_email;
        END IF;
        

        CASE l_request_type
            WHEN 'magic' THEN
                l_token:=apex_jwt.ENCODE (
                    p_iss       => 'ORDS.'||pWebsiteId,
                    p_sub       => l_email,
                    p_aud       => 'xxx',
                    p_iat_ts    => current_timestamp,
                    p_exp_sec   => ACCESS_TOKEN_EXP,
                    p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );
                l_token_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, token, domain) VALUES (l_email, 'AUTH WAITING', l_token_hash, l_domain) RETURNING id INTO l_user_id;
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', token=l_token_hash, domain=l_domain, updated_date=current_timestamp WHERE id=l_user_id;
                END IF;
                
                l_sendmail_link:=pck_core.getRestUrl || 'authenticate/' || pWebsiteId || '?request=magic&user=' || l_user_id || '&verify=' || l_token || '&domain=' || NVL(l_domain,'NO');
                l_sendmail_body:=pck_email.emailTemplate(l_request_type, l_url, l_sendmail_link);

            WHEN 'passcode' THEN
                FOR i IN 1..6 LOOP
                    l_passcode:=l_passcode || SUBSTR(l_digits,FLOOR(DBMS_RANDOM.VALUE(1, LENGTH(l_digits) + 1)),1);
                END LOOP;

                /* Store passcode as hash for later comparison with user-entered value */
                l_passcode_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_passcode,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

                IF (l_user_id=0) THEN
                    INSERT INTO users (email, login_status, passcode, domain) VALUES (l_email, 'AUTH WAITING', l_passcode_hash, l_domain);
                ELSE
                    UPDATE users SET login_status='AUTH WAITING', passcode=l_passcode_hash, domain=l_domain, updated_date=current_timestamp WHERE id=l_user_id;
                END IF;

                l_sendmail_body:=pck_email.emailTemplate(l_request_type, l_url, l_passcode);
        END CASE;

        pck_email.sendmail(l_email, 'Login to ' || l_url, l_sendmail_body);

        IF (l_request_type='passcode') THEN
            l_return_message:='Passcode sent to your inbox';
        ELSE
            l_return_message:='Link sent to your inbox';
        END IF;
        
        APEX_JSON.open_object;
        APEX_JSON.write ('success', TRUE);
        APEX_JSON.write ('message', l_return_message);
        APEX_JSON.write ('userid', l_user_id);
        APEX_JSON.close_object;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** DETERMINE ROLE (I.E. SCOPE) OF LOGGED ON USER
    */
    FUNCTION getScope(pWebsiteId IN website.id%type, pEmail IN VARCHAR2) RETURN VARCHAR2
    IS
        l_scope VARCHAR2(7);
    BEGIN
        FOR C IN (
            SELECT 'admin' as scope 
              FROM apex_workspace_apex_users
             WHERE is_admin='Yes'
               AND email=pEmail
        ) LOOP
            l_scope:=C.scope;
        END LOOP;

        IF (l_scope IS NULL) THEN
            FOR C IN (
                SELECT 'owner' as scope
                  FROM website w
                 WHERE w.id=pWebsiteId
                   AND w.contact_email=pEmail) LOOP
                l_scope:=C.scope;
            END LOOP;
        END IF;
        
        RETURN (NVL(l_scope,'visitor'));
    END;

    /*
    ** BUILD LIST OF MENU ITEMS FOR GIVEN SCOPE AND URL
    */
    FUNCTION getMenulist(pWebsiteId IN website.id%type, pScope IN VARCHAR2, pUrl IN VARCHAR2) RETURN VARCHAR2 IS
        l_menulist LONG;
        l_reports LONG;
        l_test_site BOOLEAN:=FALSE;
        l_netlify_site_id_custom website.netlify_site_id_custom%type;
        l_can_publish VARCHAR2(3);
        n PLS_INTEGER;

        FUNCTION addButton(pUriTemplate IN VARCHAR2, pMethod IN VARCHAR2, pLabel IN VARCHAR2) RETURN VARCHAR2 IS
            l_class VARCHAR2(30);
        BEGIN
            l_class:=LOWER(REPLACE(pLabel,' ','-'));
            RETURN ('<li><button type="button" class="button ' || l_class || '"data-endpoint="' || pUriTemplate || '" data-method="' ||pMethod || '" data-reports="' || l_reports || '">' || pLabel || '</button></li>');
        END;

    BEGIN
        /*
        ** ENABLE ALL EDITING AND REPORTING FUNCTIONS ON TEST SITE 
        ** ENABLE REPORTING AND PUBLISH (IF QUALIFYING) ON LIVE SITE 
        */
        l_test_site:=INSTR(pUrl,'netlify.app')>0;

        SELECT netlify_site_id_custom, CASE WHEN netlify_site_id_custom IS NOT NULL AND NVL(netlify_last_published_custom,created_date) < netlify_last_published THEN 'OK'  ELSE 'NOK' END 
          INTO l_netlify_site_id_custom, l_can_publish
          FROM website
         WHERE id=pWebsiteId;

        refreshOrdsEndpoints(pWebsiteId);

        FOR C IN (
            SELECT o.label, o.uri_template, o.method, o.priority
              FROM api_endpoint a, role r, ords_endpoints o
             WHERE a.website_id=pWebsiteId 
               AND a.role_id=r.id 
               AND r.name=pScope
               AND a.template_id=o.template_id
               AND INSTR(o.uri_template,'authenticate')=0
             ORDER BY DECODE(o.label,'Publish Website',1,'Website Analytics',2,'Upload Media',3,'Edit Header',4,'Edit Content',5,'Edit Footer',6,'Edit Pages',7,8)
        )
        LOOP
            l_reports:=NULL;
            IF (INSTR(C.uri_template,'visits/')>0) THEN
                l_reports:='visits|table;trends|graph;performance|graph;engagement|table;location|table;browser|table';
            END IF;
            IF (l_test_site) THEN
                l_menulist:=l_menulist || addButton(C.uri_template, C.method, C.label);
            ELSIF (C.priority=1) THEN
                IF (INSTR(C.uri_template,'publish-website')>0 AND l_can_publish='NOK') THEN
                    CONTINUE;
                ELSE
                   l_menulist:=l_menulist || addButton(C.uri_template, C.method, C.label);
                END IF;
            END IF;
        END LOOP;

        FOR C IN (SELECT netlify_site_id_custom, netlify_last_published_custom, netlify_last_published
                    FROM website
                   WHERE id=pWebsiteId)
        LOOP
            IF (l_test_site AND l_netlify_site_id_custom IS NOT NULL) THEN
                l_menulist:=l_menulist || '<li><a class="button" data-button-variant="nav-item" href="https://' || REPLACE(SUBSTR(pUrl,1,INSTR(pUrl,'.',1,1)-1),'-','.') || '">Live Site</a></li>';
            END IF;
            IF NOT (l_test_site) THEN
                l_menulist:=l_menulist || '<li><a class="button" data-button-variant="nav-item" href="https://' || REPLACE(pUrl,'.','-') || '.netlify.app' || '">Edit Site</a></li>';
            END IF;
        END LOOP;

        RETURN (l_menulist);
    END;

    /*
     **  Build Content Dialog - include pagination button
     */
    FUNCTION buildContentDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="output">' ||
                '<form>' ||
                    '<header class="flex-items space-between no-wrap">' ||
                        '<ul role="list" class="flex-items"></ul>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg class="icon" aria-hidden="true" focusable="false">' ||
                                '<use href="#cross"></use>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow"></article>' ||
                    '<footer class="flex-items space-between">' ||
                        '<span></span>' ||
                        '<button type="button" class="button show-more visually-hidden" data-button-variant="no-styles">More >></button>' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
     **  Build basic info Dialog - here is where we show output from calling API
     */
    FUNCTION buildInfoDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="info">' ||
                '<form>' ||
                    '<header class="flex-items space-between no-wrap">' ||
                        '<h4></h4>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg class="icon" aria-hidden="true" focusable="false">' ||
                                '<use href="#cross"></use>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article></article>' ||
                    '<footer></footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
     **  Build Send Message Dialog
     */
    FUNCTION buildMessageDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="message">' ||
                '<form>' ||
                    '<header class="flex-items space-between">' ||
                        '<h2>Send Message</h2>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg class="icon" aria-hidden="true" focusable="false">' ||
                                '<use href="#cross"></use>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article class="flow">' ||
                         '<div>' ||
                            '<label for="messageInput">Message</label>' ||
                            '<textarea id="messageInput" name="message" required minlength="1" maxlength="400" rows="5" cols="40"></textarea>' ||
                            '<div class="flex-items space-between">' ||
                                '<span class="messageInput-result"></span>' ||
                                '<span class="charcounter"></span>' ||
                            '</div>' ||
                        '</div>' ||
                    '</article>' ||
                    '<footer class="flex-items space-between">' ||
                        '<button type="button" class="button send">SEND</button>' ||
                        '<span class="send-result"></span>' ||
                    '</footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
     **  Build General purpose ERROR dialog
     */
    FUNCTION buildErrorDialog RETURN VARCHAR2 IS
    BEGIN
        RETURN(
            '<dialog class="error">' ||
                '<form>' ||
                    '<header class="flex-items space-between">' ||
                        '<h4></h4>' ||
                        '<button type="button" class="button close no-focus" data-button-variant="round-icon">' ||
                            '<svg class="icon" aria-hidden="true" focusable="false">' ||
                                '<use href="#cross"></use>' ||
                            '</svg>' ||
                        '</button>' ||
                    '</header>' ||
                    '<article style="color:red"></article>' ||
                    '<footer></footer>' ||
                '</form>' ||
             '</dialog>');
    END;

    /*
    ** CALLED BY menulist GET ENDPOINT
    */
    PROCEDURE getMenulist(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        pStatus:=200;

        APEX_JSON.open_object;
        APEX_JSON.write ('success', TRUE);
        APEX_JSON.write ('menulist', getMenulist(pWebsiteId, l_session_data.scope, l_session_data.url));
        --APEX_JSON.write ('dialogs', buildContentDialog || buildInfoDialog || buildMessageDialog  || buildErrorDialog);
        APEX_JSON.close_object;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /*
    ** BUILD FINGERPRINT FOR CLIENT DEVICE
    */
    FUNCTION getFingerprint RETURN RAW IS
        l_fingerprint_string VARCHAR2(500);
        l_fingerprint fingerprint.fingerprint%type;
    BEGIN
        l_fingerprint_string:=OWA_UTIL.get_cgi_env ('REMOTE_ADDR') || OWA_UTIL.get_cgi_env('HTTP_USER_AGENT') || OWA_UTIL.get_cgi_env('timezone');
        l_fingerprint:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_fingerprint_string,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
        RETURN (l_fingerprint);
    END;

    /*
    ** CREATE WEBSITE FOR AUTHENTICATED VISITOR 
    */
    FUNCTION createWebsite(pWebsiteId IN website.id%type, pEmail IN users.email%type, pUserId IN users.id%type, pEncKey IN user_ords_clients.client_secret%type, 
                pcldCloudName IN users.cld_cloud_name%type, pCldUploadPreset IN users.cld_upload_preset%type) RETURN VARCHAR2 IS
        l_url VARCHAR2(1000);
        l_issuer VARCHAR2(15);
        l_scope VARCHAR2(7);
        l_new_token_domain VARCHAR2 (4000);
        l_new_refresh_domain VARCHAR2 (4000);
        --l_new_refresh_domain_hash refresh_token.token_issued%type;
    BEGIN
        -- create website on hosting platform returning its URL
        pck_hosting.createWebsite(pWebsiteId, pUserid, pEmail, l_url);

        -- issue JWT tokens to be attached to URL
        l_issuer:='ORDS.'|| seq_website.currval;
        l_scope:=getScope(seq_website.currval,pEmail);
        l_new_token_domain:=apex_jwt.ENCODE (
            p_iss       =>l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => ACCESS_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (pEncKey)
        ); 
        l_new_refresh_domain:=apex_jwt.ENCODE (
            p_iss       => l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => REFRESH_TOKEN_EXP,
            p_other_claims => '"cld_cloud_name":' || apex_json.stringify(pCldCloudName) || ',"cld_upload_preset":' || apex_json.stringify(pCldUploadPreset),
            p_signature_key => UTL_RAW.cast_to_raw (pEncKey)
        );

        -- attach tokens as URL search parameters in order to auto-login the requesting user 
        l_url:=l_url || '?refresh=' || l_new_refresh_domain || '&token=' || l_new_token_domain;
        
        --l_new_refresh_domain_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh_domain,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

        RETURN (l_url);
    END;

    /*
    ** USER AUTHENTICATED SUCCESSFULLY 
    ** - CREATE FINGERPRINT
    ** - ISSUE NEW PAIR OF TOKENS TO RETURN TO CLIENT
    */
    PROCEDURE issueJWTTokens(pWebsiteId IN website.id%type, pEmail IN users.email%type, pUserId IN users.id%type, pCldCloudName IN users.cld_cloud_name%type, pCldUploadPreset IN users.cld_upload_preset%type) IS 
        l_new_token VARCHAR2 (4000);
        l_new_refresh VARCHAR2 (4000);
        l_new_refresh_hash refresh_token.token_issued%type;
        l_url VARCHAR2(1000);
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_issuer VARCHAR2(15);
        l_scope VARCHAR2(7);
        l_cld_cloud_name users.cld_cloud_name%type:=pCldCloudName;
        l_cld_upload_preset users.cld_upload_preset%type:=pCldUploadPreset;
    BEGIN
        SELECT client_secret 
          INTO l_jwt_enc_key
          FROM user_ords_clients
         WHERE name=pEmail;

        IF (l_cld_cloud_name IS NULL) THEN
            SELECT u.cld_cloud_name, u.cld_upload_preset
              INTO l_cld_cloud_name, l_cld_upload_preset
              FROM users u, apex_workspace_apex_users w
             WHERE w.email=u.email
               AND w.is_admin='Yes'
             ORDER BY date_created
             FETCH FIRST ROW ONLY;
        END IF;

        l_issuer:='ORDS.'||pWebsiteId;
        l_scope:=getScope(pWebsiteId,pEmail);

       l_new_refresh:=apex_jwt.ENCODE (
            p_iss       => l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => REFRESH_TOKEN_EXP,
            p_other_claims => '"cld_cloud_name":' || apex_json.stringify(l_cld_cloud_name) || ',"cld_upload_preset":' || apex_json.stringify(l_cld_upload_preset),
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        l_new_token:=apex_jwt.ENCODE (
            p_iss       => l_issuer,
            p_sub       => pEmail,
            p_aud       => l_scope,
            p_jti       => pUserId,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => ACCESS_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (l_jwt_enc_key)
        );

        l_new_refresh_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

        INSERT INTO refresh_token(user_id, website_id, token_issued) VALUES (pUserId, pWebsiteId, l_new_refresh_hash);
        
        APEX_JSON.open_object;
        APEX_JSON.write ('success', TRUE);
        APEX_JSON.write ('token', l_new_token);
        APEX_JSON.write ('refresh', l_new_refresh);
        APEX_JSON.write ('menulist', getMenulist(pWebsiteId, l_scope, OWA_UTIL.get_cgi_env('url')));
        APEX_JSON.write ('dialogs',  buildContentDialog || buildInfoDialog || buildMessageDialog || buildErrorDialog);
        APEX_JSON.close_object;
            
    END;

    /*
    ** VERIFY PASSCODE OR TOKEN SENT BY USER MATCHES THOSE ORIGINALLY SENT BY EMAIL
    ** SET STATUS IN USERS TABLE TO "AUTH OK"
    **  - FOR VERIFIED PASSCODE, RETURN JWT ACCESS AND REFRESH TOKENS
    **  - FOR VERIFIED TOKEN, MESSAGE IS SENT IN NEW WINDOWS TAB
    */
    PROCEDURE verifyAuthRequest(pWebsiteId IN website.id%type, pRequestType IN VARCHAR2, pUserId IN NUMBER, pVerifyData IN VARCHAR2, pDomain IN VARCHAR2, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_domain users.domain%type;
        l_cld_cloud_name users.cld_cloud_name%type;
        l_cld_upload_preset users.cld_upload_preset%type;
        l_jwt_enc_key user_ords_clients.client_secret%type;
        l_verify_data_hash RAW(32);
        l_token APEX_JWT.T_TOKEN;
        l_token_issued refresh_token.token_issued%type;
        l_url VARCHAR2(1000);
        l_url_base VARCHAR2(100);
        l_sendmail_body LONG;
    BEGIN
        /* 
        ** Check issued and received tokens are the same 
        */        
        FOR C IN (
            SELECT u.email, u.cld_cloud_name, u.cld_upload_preset, DECODE(pRequestType,'magic',token,'passcode',passcode) original_data_hash, o.client_secret 
              FROM users u, user_ords_clients o
             WHERE u.id=pUserId
               AND o.name=u.email
        ) 
        LOOP
            l_email:=C.email;
            l_cld_cloud_name:=C.cld_cloud_name;
            l_cld_upload_preset:=C.cld_upload_preset;
            l_verify_data_hash:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(pVerifyData,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);
            l_jwt_enc_key:=C.client_secret;
            IF (l_verify_data_hash<>C.original_data_hash) THEN
                CASE pRequestType
                    WHEN 'passcode' THEN
                        APEX_JSON.open_object;
                        APEX_JSON.write ('success', FALSE);
                        APEX_JSON.write ('message', 'Passcode does not match');
                        APEX_JSON.close_object;
                    WHEN 'magic' THEN
                        htp.p('<h1>Token expired. Login again.</h1>');
                END CASE;
                RETURN;
            END IF;
        END LOOP;
        
        CASE pRequestType
            /* 
            ** Decode and Validate the token returned from email client
            */
            WHEN 'magic' THEN
                
                l_token:=apex_jwt.DECODE (
                    p_value   => pVerifyData,
                    p_signature_key   => UTL_RAW.cast_to_raw (l_jwt_enc_key)
                );

                BEGIN
                    apex_jwt.VALIDATE (p_token => l_token, p_iss => 'ORDS.'||pWebsiteId);
                    EXCEPTION 
                        WHEN VALUE_ERROR THEN
                            htp.p('<h1>Token expired or not current. Check you sent from most recent message received. Login again</h1>');
                            RETURN;
                        WHEN OTHERS THEN 
                            pck_core.log(sqlerrm);
                            htp.p('<h1>' || sqlerrm || '</h1>');
                            RETURN;
                END;
                
                IF (pDomain='NO') THEN
                    htp.p('<h1 style="font-family:system-ui;font-size:2rem;color:rebeccapurple">Authenticated Successfully</h1>');
                ELSE
                    l_url:=createWebsite(pWebsiteId, l_email, pUserid, l_jwt_enc_key, l_cld_cloud_name, l_cld_upload_preset);
                    l_url_base:=SUBSTR(l_url,1,INSTR(l_url,'?')-1);
                    l_sendmail_body:=pck_email.emailTemplate('website', l_url_base);
                    pck_email.sendmail(l_email, 'Link to your website ' || l_url_base, l_sendmail_body);
                    htp.p('<div style="display:grid;place-items:center;block-size:30dvh">');
                    htp.p('<h1 style="font-family:system-ui;font-size:5vmin;color:rebeccapurple">WEBSITE CREATED</h1>');
                    htp.p('<a style="font-family:system-ui;font-size:5vmin;align-self:start;color:rebeccapurple;text-decoration:underline;text-underline-offset:25%;text-decoration-thickness:15%;" href="' || l_url || '">' || l_url_base || '</a>');
                    htp.p('</div>');
                END IF;
            
            WHEN 'passcode' THEN
                IF (pDomain='NO') THEN
                    issueJWTTokens(pWebsiteId, l_email, pUserId, l_cld_cloud_name, l_cld_upload_preset);
                ELSE
                    l_url:=createWebsite(pWebsiteId, l_email, pUserid, l_jwt_enc_key, l_cld_cloud_name, l_cld_upload_preset);
                    l_url_base:=SUBSTR(l_url,1,INSTR(l_url,'?')-1);
                    l_sendmail_body:=pck_email.emailTemplate('website', l_url_base);
                    pck_email.sendmail(l_email, 'Link to your website ' || l_url_base, l_sendmail_body);
                    APEX_JSON.open_object;
                    APEX_JSON.write ('success', TRUE);
                    APEX_JSON.write ('url', l_url);
                    APEX_JSON.close_object;
                END IF;
        END CASE;

        UPDATE users SET login_status='AUTH OK', last_login_date=current_timestamp, last_login_provider='JWT' WHERE id=pUserId;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

    /* 
    ** CLIENT HAS ClICKED ON MAGIC LINK ON MESSAGE RECEIVED IN THEIR INBOX,
    ** CAUSING THEIR USERS.LOGIN_STATUS TO BE UPDATED TO "AUTH OK".
    ** NOW WE CAN ISSUE NEW PAIR OF TOKENS
    */
    PROCEDURE checkAuthStatus(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_email users.email%type;
        l_user_id users.id%type;
        l_cld_cloud_name users.cld_cloud_name%type;
        l_cld_upload_preset users.cld_upload_preset%type;
        l_time_since_sent number;
        l_token_issued refresh_token.token_issued%type;
    BEGIN
        SELECT u.id, LOWER(u.email), u.cld_cloud_name, u.cld_upload_preset, extract (minute from (current_timestamp - coalesce(u.updated_date, u.created_date)))
          INTO l_user_id, l_email, l_cld_cloud_name, l_cld_upload_preset, l_time_since_sent
          FROM users u, JSON_TABLE(pBodyText, '$' COLUMNS email) j
         WHERE u.email=j.email
           AND u.login_status='AUTH OK';

        issueJWTTokens(pWebsiteId, l_email, l_user_id, l_cld_cloud_name, l_cld_upload_preset);

        pStatus:=200;

        EXCEPTION 
            WHEN NO_DATA_FOUND THEN
                APEX_JSON.open_object;
                APEX_JSON.write ('success', TRUE);
                IF (l_time_since_sent * 60 >= ACCESS_TOKEN_EXP) THEN
                    APEX_JSON.write ('expired', TRUE);
                END IF;
                APEX_JSON.close_object;

            WHEN OTHERS THEN
                pck_core.log_error(pStatus);
    END;

        
    /*
    ** CREATE AND RETURN NEW ACCESS / REFRESH TOKEN PAIR
    ** CALLED WHEN CLIENT'S ACCESS TOKEN HAS EXPIRED
    ** CHECK REFRESH TOKEN HAS NOT PREVIOUSLY BEEN USED
    */
    PROCEDURE refreshAuthToken(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS 
        l_refresh_token VARCHAR2(4000);
        l_token_used refresh_token.token_used%type;
        l_token_issued refresh_token.token_issued%type;
        l_token APEX_JWT.T_TOKEN;
        l_new_token VARCHAR2 (4000);
        l_new_refresh VARCHAR2 (4000);
        l_fingerprint fingerprint.fingerprint%type;
        n PLS_INTEGER;
        l_JWT_data t_JWT_data;
    BEGIN
        l_refresh_token:=SUBSTR(OWA_UTIL.get_cgi_env('Authorization'), 8);
        l_JWT_data:=decodeJWT(l_refresh_token);

        -- Check user's fingerprint is known
        l_fingerprint:=getFingerprint;
        SELECT COUNT(*) 
          INTO n
          FROM fingerprint
         WHERE user_id=l_JWT_data.jti
           AND fingerprint=l_fingerprint;

        --IF (n=0) THEN
        --    RAISE_APPLICATION_ERROR(-20000,'Access denied from previously unknown device or location. Login again.');
        --END IF;

        l_token:=apex_jwt.DECODE (p_value => l_refresh_token, p_signature_key => UTL_RAW.cast_to_raw(l_JWT_data.client_secret));

        BEGIN
            apex_jwt.VALIDATE (p_token => l_token, p_iss => l_JWT_data.iss);
            EXCEPTION WHEN VALUE_ERROR THEN
                pck_core.log('token invalid');
                RAISE_APPLICATION_ERROR(-20000,'token invalid');
        END;

        -- Check refresh token has not previously been used to generate a new token pair  (nb : all tokens stored as hash values)
        l_token_used:=dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_refresh_token,'AL32UTF8'), typ=>dbms_crypto.hash_sh256);

        l_new_token:=apex_jwt.ENCODE (
            p_iss       => l_JWT_data.iss,
            p_sub       => l_JWT_data.sub,
            p_aud       => l_JWT_data.aud,
            p_jti       => l_JWT_data.jti,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => ACCESS_TOKEN_EXP,
            p_signature_key => UTL_RAW.cast_to_raw (l_JWT_data.client_secret)
        );

        l_new_refresh:=apex_jwt.ENCODE (
            p_iss       => l_JWT_data.iss,
            p_sub       => l_JWT_data.sub,
            p_aud       => l_JWT_data.aud,
            p_jti       => l_JWT_data.jti,
            p_iat_ts    => current_timestamp,
            p_exp_sec   => REFRESH_TOKEN_EXP,
            p_other_claims => '"cld_cloud_name":' || apex_json.stringify(l_JWT_data.cld_cloud_name) ||
                              ',"cld_upload_preset":' || apex_json.stringify(l_JWT_data.cld_upload_preset),
            p_signature_key => UTL_RAW.cast_to_raw (l_JWT_data.client_secret)
        );

        INSERT INTO refresh_token(website_id, user_id, token_used, token_issued)
            VALUES (pWebsiteid, l_JWT_data.jti, l_token_used, dbms_crypto.hash(src=>UTL_I18N.STRING_TO_RAW(l_new_refresh,'AL32UTF8'), typ=>dbms_crypto.hash_sh256));

        APEX_JSON.open_object;
        APEX_JSON.write ('success', TRUE);
        APEX_JSON.write ('token', l_new_token);
        APEX_JSON.write ('refresh', l_new_refresh);
        APEX_JSON.close_object;

        pStatus:=200;

        EXCEPTION WHEN OTHERS THEN
            pck_core.log_error(pStatus);
    END;

  
    /*
    ** This procedure is called in the Post-authentication of Social Sign-in processing.
    ** First-time authenticated users are inserted into the USERS table
    */
    PROCEDURE insert_user_login(p_auth_provider IN VARCHAR2, p_email IN VARCHAR2, p_name IN VARCHAR2) IS
        l_user_id users.id%type;
        --l_user_name users.name%type;
        --l_client_tz users.client_tz%type;
        --l_client_tz_session users.client_tz%type:=apex_util.get_session_state('APP_CLIENT_TZ');
        l_workspace_id apex_workspaces.workspace_id%type;
    BEGIN
        SELECT workspace_id
          INTO l_workspace_id
          FROM apex_workspaces
         WHERE workspace = (
                SELECT workspace
                FROM apex_applications
                WHERE application_id = APEX_APPLICATION.G_FLOW_ID
                );
        /*
        FOR C IN (
            SELECT apex_session_id
              FROM apex_workspace_sessions
             WHERE workspace_id = l_workspace_id
               AND user_name = APEX_APPLICATION.G_USER
               AND apex_session_id != APEX_APPLICATION.G_INSTANCE
        )
        LOOP
            apex_session.delete_session(p_session_id => C.apex_session_id);
            DELETE session_data WHERE session_id=C.apex_session_id;
        END LOOP;
        */

        INSERT INTO user_login(auth_provider,email,name,apex_session_id) VALUES (p_auth_provider, p_email, p_name, APEX_APPLICATION.G_INSTANCE);

        BEGIN
            SELECT id
              INTO l_user_id
              FROM users 
             WHERE email=p_email;

            UPDATE users 
               SET name=p_name, last_login_date=current_timestamp, last_login_provider=p_auth_provider 
             WHERE id=l_user_id;

            EXCEPTION WHEN NO_DATA_FOUND 
            THEN
                INSERT INTO users(email, name, last_login_date, last_login_provider) 
                VALUES (p_email, p_name, current_timestamp, p_auth_provider);
        END;
        COMMIT;
    END;    

    PROCEDURE post_auth_facebook IS
    BEGIN
        insert_user_login('Facebook', apex_json.get_varchar2('email'), apex_json.get_varchar2('name'));
        EXCEPTION WHEN OTHERS THEN pck_core.log_error; RAISE;
    END;

    PROCEDURE post_auth_google IS
    BEGIN
        insert_user_login('Google', apex_json.get_varchar2('email'), apex_json.get_varchar2('name'));
        EXCEPTION WHEN OTHERS THEN pck_core.log_error; RAISE;
    END; 

    /* 
    ** Linked-In API has separate entry points for profile name and email addres.
    ** Support very rude. API is bollocks. Gave up on Linkedin but keep the code just in case
    */
    /*
    PROCEDURE post_auth_linkedin IS
        l_name users.name%type;
        l_email users.email%type;
        l_clob CLOB;
        l_names owa_cookie.vc_arr;
        l_vals owa_cookie.vc_arr;
        l_num_vals INTEGER;
    BEGIN
        pck_cms.log('starting'); 
        l_name:=apex_json.get_varchar2('localizedFirstName') || ' ' || apex_json.get_varchar2('localizedLastName');

        apex_web_service.g_request_headers.delete();
        pck_cms.log('Before make_rest_request');
        l_clob := apex_web_service.make_rest_request(
            p_url => 'https://api.linkedin.com/v2/emailAddress?q=members&projection=(elements*(handle~))',
            p_http_method => 'GET',
            p_credential_static_id => 'Linkedin',
            p_token_url => 'https://www.linkedin.com/oauth/v2/accessToken');
        pck_cms.log(l_clob);

        SELECT emailAddress INTO l_email 
          FROM JSON_TABLE(l_clob format json, '$'
            COLUMNS emailAddress VARCHAR2(100)  PATH '$.elements[0]."handle~".emailAddress');    

        apex_application.g_user:=l_email;

        insert_user_login('Linkedin', l_email, l_name);        
    END;   
    */

    /*
    **  Gave up on Azure too - can't remember why though.
    */
    /*
    PROCEDURE post_auth_azure IS
    BEGIN
        insert_user_login('Azure', apex_json.get_varchar2('userPrincipalName'), apex_json.get_varchar2('givenName') || ' ' || apex_json.get_varchar2('surname'));
    END; 
    */

END PCK_SEC;
/