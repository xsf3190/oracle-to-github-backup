CREATE OR REPLACE EDITIONABLE PACKAGE "PCK_EDITOR" is
    --
    PROCEDURE deleteHero(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE deployWebsite(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getDeploymentStatus(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE getFonts(pWebsiteId IN website.id%type, pCategory IN font.category%type, pFontId IN font.id%type, pContext IN website_font.context%type, pStatus OUT NUMBER);
    --
    FUNCTION getFontURL(pFontid IN font.id%type) RETURN VARCHAR2;
    --
    PROCEDURE getFooter(pWebsiteId IN website.id%type, pEyeDropper IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE getHeader(pWebsiteId IN website.id%type, pEyeDropper IN VARCHAR2, pStatus OUT NUMBER);
    --
    PROCEDURE getPages(pWebsiteId IN website.id%type, pStatus OUT NUMBER);
    --
    PROCEDURE updateEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updateFooter(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updateHeader(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updateHero(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
    PROCEDURE updatePages(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER);
    --
end;
/
CREATE OR REPLACE EDITIONABLE PACKAGE BODY "PCK_EDITOR" is

    TYPE tt_labels IS RECORD (
            article_id website_article.article_id%type,
            navigation_label website_article.navigation_label%type,
            display_order website_article.display_order%type
        );
    TYPE t_labels IS TABLE OF tt_labels;

    /* 
    **  DEPLOY WEBSITE
    */
    PROCEDURE deployWebsite(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_job_name VARCHAR2(30);
        l_env VARCHAR2(4);
        l_site_id website.netlify_site_id%type;
        l_site_id_custom website.netlify_site_id_custom%type;
        l_src_last_updated es_modules.updated_source%type;
        l_cdn_last_updated es_modules.updated_test%type;
        l_esm_library VARCHAR2(4):='live';
    BEGIN
        /* 
        ** Check no other session currently deploying - simultaneous deployments not allowed on Netlify free plan 
        */
        FOR C IN (SELECT job_name FROM user_scheduler_running_jobs WHERE job_name LIKE 'DEPLOY_%') LOOP
            apex_json.open_object;
            apex_json.write('content', '<h4>Someone else is deploying ... try again later</h4>');
            apex_json.write('stop', TRUE);
            apex_json.write('success', TRUE);
            apex_json.close_object;
            RETURN;
        END LOOP;

        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        /* 
        ** Rebuild es-modules CDN if Admin user deploying their test site AND last source updated date is after last CDN updated_date
        */
        FOR C IN (
            SELECT null
              FROM website w, users u, apex_workspace_apex_users a
             WHERE w.id=pWebsiteId
               AND u.id=l_session_data.user_id
               AND w.domain_name=REPLACE(SUBSTR(u.email,1,INSTR(u.email,'@')-1),'.','-')
               AND u.email=a.email
               AND a.is_admin='Yes'
         ) 
         LOOP
            l_esm_library:='test';

            SELECT MAX(CAST(last_updated_on AS TIMESTAMP WITH TIME ZONE)), MAX(updated_test)
              INTO l_src_last_updated, l_cdn_last_updated
              FROM apex_application_static_files s, es_modules e
             WHERE s.file_name=e.module_name
               AND s.application_id=101;

            IF (l_src_last_updated>l_cdn_last_updated) THEN
                pck_deploy.buildESModules;
            END IF;
        END LOOP;

        l_env:=CASE WHEN INSTR(l_session_data.url,'netlify.app')>0 THEN 'TEST' ELSE 'LIVE' END;

        UPDATE website SET netlify_deploy_id=NULL WHERE id=pWebsiteId RETURNING netlify_site_id, netlify_site_id_custom INTO l_site_id, l_site_id_custom;
        
        DELETE website_deploy WHERE website_id=pWebsiteId AND site_id=CASE WHEN l_env='TEST' THEN l_site_id ELSE l_site_id_custom END;
        
        l_job_name:=dbms_scheduler.generate_job_name('DEPLOY_');
        dbms_scheduler.create_job(
            job_name   => l_job_name,
            job_type   =>'STORED_PROCEDURE',
            job_action =>'pck_deploy.runDeployment',
            number_of_arguments=>6,
            start_date=>systimestamp
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 1,
            argument_value => pWebsiteId
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 2,
            argument_value => l_session_data.user_id
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 3,
            argument_value => l_env
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 4,
            argument_value => CASE WHEN l_env='TEST' THEN l_site_id ELSE l_site_id_custom END
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 5,
            argument_value => pck_core.getRestUrl()
        );
        dbms_scheduler.set_job_argument_value(
            job_name => l_job_name,
            argument_position => 6,
            argument_value => l_esm_library
        );
        dbms_scheduler.enable(l_job_name);

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('content', '<table role="presentation" style="font-family:monospace;background:#342a21;color:white"><tbody><tr><td>' || TO_CHAR(current_timestamp AT TIME ZONE l_session_data.timezone,'hh24:mi:ss') || '</td><td>Starting deployment</td></tr></tbody></table>');
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /*
     ** Get deployment status from website_deploy logging table - called from Javascript every 3 seconds
     ** Have to poll Netlify deployment status 
     */
    PROCEDURE getDeploymentStatus(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS 
        l_site_id website_deploy.site_id%type;
        l_content CLOB;
        l_clob CLOB;
        l_deployment_complete BOOLEAN:=FALSE;
        l_netlify_status website_deploy.status%type;
        l_netlify_deploy_id website.netlify_deploy_id%type;
        l_state VARCHAR2(200);
        l_job_name VARCHAR2(30);
        l_session_data pck_sec.t_session_data;
        TYPE t_deploy_log IS RECORD(
            id website_deploy.id%type,
            log_time website_deploy.log_time%type,
            message website_deploy.message%type,
            status website_deploy.status%type);
        TYPE tt_deploy_log IS TABLE OF t_deploy_log;
        l_deploy_log tt_deploy_log;          
        l_env VARCHAR2(4);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        IF (INSTR(l_session_data.url,'netlify.app')>0) THEN
            l_env:='TEST';
        ELSE
            l_env:='LIVE';
        END IF;

        SELECT netlify_deploy_id, CASE WHEN l_env='TEST' THEN netlify_site_id ELSE netlify_site_id_custom END
          INTO l_netlify_deploy_id, l_site_id
          FROM website
         WHERE id=pWebsiteId;

        /* Get latest deployment status if started */
        IF (l_netlify_deploy_id IS NOT NULL) THEN
            pck_api.callNetlifyAPI(pUserId=>l_session_data.user_id, pEndpoint=>'sites/'|| l_site_id || '/deploys/' || l_netlify_deploy_id, pMethod=>'GET', pData=>l_clob);
            SELECT 'Netlify is ' || state INTO l_state FROM JSON_TABLE(l_clob, '$' COLUMNS (state)); 
            pck_deploy.logDeployment(pWebsiteId, l_site_id, l_state, pLogTime=>current_date);
        END IF;

        SELECT id, log_time, message, status
          BULK COLLECT INTO l_deploy_log
          FROM website_deploy 
         WHERE website_id=pWebsiteId
           AND site_id=l_site_id
           AND sent_ind='N'
         ORDER BY id;

        /* Now return list of all deployment status messages to client */
        FOR i IN 1..l_deploy_log.COUNT
        LOOP
            l_content:=l_content ||'<tr' || CASE WHEN l_deploy_log(i).status='NOK' THEN ' style="color:red"' END || '><td>' || TO_CHAR(l_deploy_log(i).log_time AT TIME ZONE l_session_data.timezone,'hh24:mi:ss') || '</td><td>' || l_deploy_log(i).message || '</td></tr>'; 
            IF (l_deploy_log(i).message='Netlify is ready') THEN 
                l_content:=l_content ||'<tr style="color:green"><td>' || TO_CHAR(l_deploy_log(i).log_time AT TIME ZONE l_session_data.timezone,'hh24:mi:ss') || '</td><td>SUCCESSFUL DEPLOYMENT</td></tr>'; 
                l_deployment_complete:=TRUE;
            END IF;
        END LOOP;

        IF (l_deployment_complete) THEN
            update article 
            set deployed_date=case when l_env='TEST' THEN current_timestamp ELSE deployed_date end,
                deployed_date_custom=case when l_env='LIVE' THEN current_timestamp ELSE deployed_date_custom end
            where id in
            (
            select id from article
            connect by prior id=parent_id
            start with id in (select article_id from website_article where website_id=pWebsiteid)
            );
        END IF;

        apex_json.open_object; 
        apex_json.write('success', TRUE);
        apex_json.write('completed', l_deployment_complete);
        apex_json.write('content', l_content);
        apex_json.close_object;

        FORALL i IN 1..l_deploy_log.COUNT
            UPDATE website_deploy SET sent_ind='Y' WHERE id=l_deploy_log(i).id;

        IF (l_deployment_complete) THEN
            IF (l_env='LIVE') THEN
                UPDATE website 
                   SET netlify_last_published_custom = current_timestamp
                 WHERE id=pWebsiteId;
            ELSE
                UPDATE website 
                   SET netlify_last_published = current_timestamp
                 WHERE id=pWebsiteId;
            END IF;

            l_job_name:=dbms_scheduler.generate_job_name('DELETE_');
            dbms_scheduler.create_job(
                job_name   => l_job_name,
                job_type   =>'STORED_PROCEDURE',
                job_action =>'pck_deploy.runDelete',
                number_of_arguments=>3,
                start_date=>systimestamp
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 1,
                argument_value => l_session_data.user_id
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 2,
                argument_value => pWebsiteId
            );
            dbms_scheduler.set_job_argument_value(
                job_name => l_job_name,
                argument_position => 3,
                argument_value => l_site_id
            );
            dbms_scheduler.enable(l_job_name);    
        END IF;

        pStatus:=200;

        EXCEPTION
            WHEN OTHERS THEN
                pck_core.log_error(pStatus);
    END;

    /* 
    **  BUILD FORM CONTROLS TO EDIT WEBSITE PAGES
    */
    PROCEDURE getPages(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_html LONG;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
            
        l_html:=
            '<fieldset class="flex-items">' ||
                '<legend><strong>Change / Add / Delete Pages</strong></legend>' ||
                '<div>' ||
                    '<label for="navigation-label">Navigation Label</label>' ||
                    '<input type="text" id="navigation-label" name="navigation_label" maxlength="30" value="">' ||
                '</div>' ||
                '<fieldset class="flex-items">' ||
                    '<legend>Page Type</legend>' ||
                    '<label for="collection_type-normal">Normal' ||
                    '<input type="radio" id="collection_type-normal" name="collection_type" value="N/A"></label>' ||
                    '<label for="collection_type-blog">Blog' ||
                    '<input type="radio" id="collection_type-blog" name="collection_type" value="BLOG"></label>' ||
                    '<label for="collection_type-portfolio">Portfolio' ||
                    '<input type="radio" id="collection_type-portfolio" name="collection_type" value="PORTFOLIO"></label>' ||
                '</fieldset>' ||

                '<button type="button" class="button add-page">Add Page</button>' ||
                '<button type="button" class="button delete-page">Delete Page</button>' ||
            '</fieldset>' ||
            '<ul class="flex-items">' ||
                '<li><button type="button" class="button publish-changes" data-endpoint="publish-website/" style="--button-bg:rebeccapurple">PUBLISH</button></li>' ||
                '<li><button type="button" class="button cancel-changes" style="--button-bg:rebeccapurple">CANCEL</button></li>' ||
            '</ul>';

        apex_json.open_object;
        apex_json.write('success', TRUE); 
        apex_json.write('html', l_html);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  UPDATE WEBSITE_ARTICLE TABLE WITH ALL CHANGES 
    */
    PROCEDURE updatePages(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        TYPE tt_arr IS RECORD(
            display_order website_article.display_order%type,
            article_id website_article.article_id%type,
            navigation_label website_article.navigation_label%type,
            collection_type website_article.collection_type%type
        );
        TYPE t_arr IS TABLE OF tt_arr INDEX BY PLS_INTEGER;
        l_arr t_arr;
        l_current_date TIMESTAMP:=current_timestamp;
        l_moved_article_id website_article.article_id%type;
        TYPE t_article_id IS TABLE OF website_article.article_id%type;
        l_article_id t_article_id;
        l_clob CLOB;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        SELECT display_order, article_id, navigation_label, collection_type
          BULK COLLECT INTO l_arr
          FROM JSON_TABLE(pBodyText, '$[*]' COLUMNS (display_order FOR ORDINALITY, article_id, navigation_label, collection_type));

       -- Create ARTICLE rows for new pages. Retain rows where labels have been moved.
        FOR i IN 1..l_arr.COUNT LOOP
            IF (l_arr(i).article_id<=0) THEN
                FOR C IN (
                    SELECT article_id FROM website_article WHERE website_id=pWebsiteId AND navigation_label=l_arr(i).navigation_label
                ) LOOP
                    l_moved_article_id:=C.article_id;
                    l_arr(i).article_id:=C.article_id;
                END LOOP;
                IF (l_moved_article_id IS NULL) THEN
                    INSERT INTO article (id, body_html, author_user_id) 
                    VALUES (seq_article.nextval, '<article class="flow"></article>', l_session_data.user_id);
                    INSERT INTO website_article (website_id, article_id, navigation_label, collection_type, user_id, display_order)
                    VALUES (pWebsiteId, seq_article.currval, l_arr(i).navigation_label, l_arr(i).collection_type, l_session_data.user_id, 1);
                    l_arr(i).article_id:=seq_article.currval;
                END IF;
            END IF;
        END LOOP;

        FORALL i IN l_arr.FIRST .. l_arr.LAST
            UPDATE website_article 
               SET display_order=l_arr(i).display_order,
                   navigation_label=l_arr(i).navigation_label,
                   collection_type=l_arr(i).collection_type,
                   updated_date=l_current_date
             WHERE website_id = pWebsiteId
               AND article_id=l_arr(i).article_id;

        -- Delete pages not passed from client
        DELETE website_article WHERE website_id=pWebsiteId AND updated_date<l_current_date RETURNING article_id BULK COLLECT INTO l_article_id;

        -- Delete Cloudinary assets tagged by article_id
        FOR i IN 1..l_article_id.COUNT LOOP
            FOR C IN (
                SELECT DISTINCT cld_cloud_name, resource_type
                  FROM asset
                 WHERE article_id=l_article_id(i)
            )   
            LOOP
                pck_api.callCloudinaryAPI(pUserId=>l_session_data.user_id, pEndpoint=>C.cld_cloud_name || '/resources/' || C.resource_type || '/tags/' || l_article_id(i), pMethod=>'DELETE', pData=>l_clob);
            END LOOP;
        END LOOP;

        FORALL i IN l_article_id.FIRST .. l_article_id.LAST
            DELETE article WHERE id=l_article_id(i);

        apex_json.open_object;
        apex_json.write('success', TRUE);
        apex_json.write('deleted',l_article_id.COUNT);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* 
    **  GET CONTENT FOR CKEDITOR
    */
    PROCEDURE getEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_body_html article.body_html%type;
        l_last_update VARCHAR2(20);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        SELECT body_html, apex_util.get_since(updated_date) last_update
          INTO l_body_html, l_last_update
          FROM article
         WHERE id=pArticleId;

        apex_json.open_object;
        apex_json.write('success', TRUE); 
        apex_json.write('html', l_body_html);
        apex_json.write('last_update', l_last_update);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;



    /* 
    **  UPDATE CONTENT RECEIVED FROM CKEDITOR
    */
    PROCEDURE updateEditorContent(pWebsiteId IN website.id%type, pArticleId IN article.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_word_count article.word_count%type;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        UPDATE article t SET (t.word_count, t.body_html, t.updated_date) = 
            (
                SELECT REGEXP_SUBSTR(j.word_count,'(\d)+') word_count, j.body_html, current_timestamp as updated_date
                  FROM article x, JSON_TABLE(pBodyText FORMAT JSON, '$' COLUMNS (word_count, body_html CLOB)) j
                 WHERE x.id=pArticleId
            )
        WHERE t.id=pArticleId;
        --RETURNING t.word_count INTO l_word_count;

        APEX_JSON.open_object; 
        APEX_JSON.write('success', TRUE);
        APEX_JSON.write('message', 'Saved');
        APEX_JSON.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  RETURN GOOGLE FONTS URL FOR USER-SELECTED FONT
    */
    FUNCTION getFontURL(pFontid IN font.id%type) RETURN VARCHAR2 IS
        l_api_url VARCHAR2(250);
        l_url VARCHAR2(500);
        l_url_regex VARCHAR2(50):='url.*;';
        l_font_css CLOB;
        n PLS_INTEGER;
    BEGIN
        FOR C IN (
                SELECT f.family, f.variable,
                    LTRIM(NVL2(f.ital_start,'ital',NULL) || NVL2(f.opsz_start,',opsz',NULL) || NVL2(f.slnt_start,',slnt',NULL) || 
                        NVL2(f.wdth_start,',wdth',NULL) || NVL2(f.wght_start,',wght',NULL),',') axes,
                    LTRIM(NVL2(f.opsz_start,f.opsz_start||'..'||f.opsz_end,NULL) || NVL2(f.slnt_start,','||f.slnt_start||'..'||f.slnt_end,NULL) || 
                        NVL2(f.wdth_start,','||f.wdth_start||'..'||f.wdth_end,NULL) || NVL2(f.wght_start,','||f.wght_start||'..'||f.wght_end,NULL),',') ranges
                FROM font f
                WHERE f.id=pFontid) 
        LOOP
            IF (C.variable=0) THEN
                l_api_url:='https://fonts.googleapis.com/css?family=' || REPLACE(C.family,' ','+');
            ELSE
                l_api_url:='https://fonts.googleapis.com/css2?family=' || REPLACE(C.family,' ','+');
                IF (C.axes IS NOT NULL) THEN
                    l_api_url:=l_api_url || ':' || C.axes || '@';
                    IF (INSTR(C.axes,'ital')>0) THEN
                        l_api_url:=l_api_url || '0,' || C.ranges || ';1,' || C.ranges;
                    ELSE
                        l_api_url:=l_api_url || C.ranges;
                    END IF;
                END IF;
            END IF;
            pck_api.callGoogleAPI(l_api_url,pData=>l_font_css);

            /* Get url for Latin charcters ... but desperate but so far seems to be in last font-face */
            n:=regexp_count(l_font_css,l_url_regex);
            l_url:=regexp_substr(l_font_css,l_url_regex,1,n);
        END LOOP;

        RETURN(RTRIM(l_url,';'));
    END;

    /* 
    **  BUILD SELECT LIST OF FONT FAMILIES FOR GIVEN CATEGORY (if font_id=0) ELSE GET FONT SETTINGS FOR A SPECIFIC FONT
    */
    PROCEDURE getFonts(pWebsiteId IN website.id%type, pCategory IN font.category%type, pFontId IN font.id%type, pContext IN website_font.context%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_html CLOB;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        APEX_JSON.open_object; 

        IF (pFontId=0) THEN /* Get all fonts for category*/
            l_html:='<option value="">--Choose a Font--</option>';
            CASE pContext
                WHEN 'title' THEN
                    FOR C IN (
                        SELECT id, family 
                        FROM font 
                        WHERE category=pCategory ORDER BY 2
                    )
                    LOOP
                        l_html:=l_html || '<option value="' || C.id || '">' || C.family || '</option>';
                    END LOOP;
                WHEN 'subtitle' THEN
                    FOR C IN (
                        SELECT id, family 
                        FROM font 
                        WHERE category=pCategory
                          AND ital_start IS NOT NULL 
                          AND 400 BETWEEN wght_start AND wght_end
                          AND 700 BETWEEN wght_start AND wght_end
                        ORDER BY 2
                    )
                    LOOP
                        l_html:=l_html || '<option value="' || C.id || '">' || C.family || '</option>';
                    END LOOP;
            END CASE;
            APEX_JSON.write('content', l_html);
        ELSE
            FOR C IN (
                SELECT f.family, f.variable, f.ital_start, f.ital_end
                 FROM font f
                WHERE f.id=pFontid
            ) 
            LOOP
                apex_json.write('url',getFontUrl(pFontid));

                apex_json.open_array('axes');

                apex_json.open_object;
                apex_json.write('name','ital');
                apex_json.write('min', C.ital_start);
                apex_json.write('max', C.ital_end);
                apex_json.close_object;
                /*
                apex_json.open_object;
                apex_json.write('name','wght');
                apex_json.write('min', CASE WHEN C.wght_start=C.wght_end THEN NULL ELSE C.wght_start END);
                apex_json.write('max', CASE WHEN C.wght_start=C.wght_end THEN NULL ELSE C.wght_end END);
                apex_json.close_object;
                */
                
                apex_json.close_array;
            END LOOP;
        END IF;

        APEX_JSON.write('success', TRUE);
        APEX_JSON.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;

    /* BUILD <select> elements for specified font */
    FUNCTION buildSelectFont(pFontId IN font.id%type, pColumnName IN VARCHAR2) RETURN VARCHAR2 IS
        l_html LONG;
        l_selected_category font.category%type;
    BEGIN
        /* Font Category first */
        SELECT category INTO l_selected_category FROM font WHERE id=pFontId;
        l_html:=
        '<label for="' || pColumnName || '-font-category">Font Category' ||
          '<select id="' || pColumnName || '-font-category" name="' || pColumnName || '_font_category" style="min-width:10em">' ||
            '<button>' ||
              '<selectedcontent></selectedcontent>' ||
              '<span class="arrow"></span>' ||
            '</button>';
        FOR C IN (SELECT DISTINCT category FROM font ORDER BY 1)
        LOOP
            l_html:=l_html || 
            '<option value="' || C.category || '"' || CASE WHEN C.category=l_selected_category THEN ' selected' END || '>' ||
                C.category ||
            '</option>';
        END LOOP;
        l_html:=l_html || 
            '</select>' ||
        '</label>';

        /* Fonts in category */
        l_html:=l_html || 
        '<label for="' || pColumnName || '-font-family">Font Family' ||
        '<select id="' || pColumnName || '-font-family" name="' || pColumnName || '_font_family">' ||
            '<button>' ||
              '<selectedcontent></selectedcontent>' ||
              '<span class="arrow"></span>' ||
            '</button>';
        CASE pColumnName
            WHEN 'title' THEN
                FOR C IN (SELECT id, family FROM font WHERE category=l_selected_category ORDER BY 2)
                LOOP
                    l_html:=l_html || 
                    '<option value="' || C.id || '"'|| CASE WHEN C.id=pFontId THEN ' selected' END || '>' ||
                        C.family ||
                    '</option>';
                END LOOP;
            WHEN 'subtitle' THEN
                FOR C IN (SELECT id, family FROM font WHERE category=l_selected_category AND ital_start IS NOT NULL AND wght_end>wght_start ORDER BY 2)
                LOOP
                    l_html:=l_html || 
                    '<option value="' || C.id || '"'|| CASE WHEN C.id=pFontId THEN ' selected' END || '>' ||
                        C.family ||
                    '</option>';
                END LOOP;
        END CASE;
        l_html:=l_html || 
            '</select>' ||
        '</label>';

        RETURN(l_html);
    END;

    /*
     **  Create wrapper html for input text element
     */
    FUNCTION buildFontWrapper(pWebsiteId IN website.id%type, pColumn IN VARCHAR2, pValue IN VARCHAR2) RETURN VARCHAR2 IS
        l_table_name user_tab_columns.table_name%type:=UPPER(SUBSTR(pColumn,1,INSTR(pColumn,'.')-1));
        l_column_name user_tab_columns.column_name%type:=SUBSTR(pColumn,INSTR(pColumn,'.')+1);
        l_html LONG;
    BEGIN
        FOR C IN (
            SELECT c.data_length, i.comments, wf.font_id, wf.font_size, wf.color, wf.underline, wf.margin, 
                --f.wght_start, f.wght_end, wf.wght, CASE WHEN NVL(f.wght_start,0)< NVL(f.wght_end,0) THEN 1 END show_wght,
                f.ital_start, f.ital_end, wf.ital
              FROM user_tab_columns c, user_col_comments i, website w, website_font wf, font f
             WHERE c.table_name=l_table_name
               AND c.column_name=UPPER(l_column_name)
               AND i.table_name=c.table_name
               AND i.column_name=c.column_name
               AND w.id=pWebsiteId
               AND wf.website_id=w.id
               AND wf.context=UPPER(l_column_name)
               AND wf.font_id=f.id
        )
        LOOP
            l_html:=
            '<fieldset class="flex-items '|| l_column_name || '-font" style="--flex-direction:column; --flex-vertical-alignment:flex-start;--flex-gap:1em">' ||
                '<legend><strong>Change ' || INITCAP(REPLACE(l_column_name,'_',' ')) || '</strong></legend>' ||
                '<div class="flex-items" style="--flex-direction:row;">' ||
                    buildSelectFont(C.font_id, l_column_name) ||
                '</div>' ||

                '<div style="--flex-direction:row;--flex-vertical-alignment:center;width:100%" class="flex-items">' ||
                    '<input type="text" style="font:inherit;font-size:120%;max-inline-size:50%" placeholder="Enter new ' || REPLACE(l_column_name,'_',' ') || '" id="' || l_column_name || '" name="' || l_column_name || 
                        '" maxlength="' || C.data_length || '" value="' || pValue || '">' ||
                    '<span class="charcounter" style="font-size:80%;margin-inline-start:0.2em">' || NVL(LENGTH(pValue),0) ||  '/' || C.data_length || '</span>' ||
                    '<label for="' || l_column_name || '-color">Color' || 
                        '<input type="color" id="' || l_column_name || '-color" name="' || l_column_name || '_color" value="' || C.color || '">' || 
                    '</label>' ||
                '</div>' ||
                
                '<div class="flex-items" style="--flex-direction:row;--flex-gap:1em">' ||
                    '<label for="' || l_column_name || '-font-size">Size' ||
                        '<input type="range" id="' || l_column_name || '-font-size" name="' || l_column_name || '_font_size" min="2" max="14" value="' || C.font_size || '"/>' ||
                    '</label>' ||
                    '<label for="' || l_column_name || '-underline">Underline' ||
                        '<input type="range" id="' || l_column_name || '-underline" name="' || l_column_name || '_underline" min="0" max="22" value="' || C.underline || '"/>' ||
                    '</label>' ||
                    '<label for="' || l_column_name || '-margin">Move' ||
                        '<input type="range" id="' || l_column_name || '-margin" name="' || l_column_name || '_margin" min="0" max="30" value="' || C.margin || '"/>' ||
                    '</label>' ||
                    --'<label for="' || l_column_name || '-font-wght"' || CASE WHEN C.show_wght<>1 THEN ' class="visually-hidden"' END || '>Weight' ||
                    --    '<input type="range" id="' || l_column_name || '-font-wght"' || CASE WHEN C.wght_start IS NULL THEN ' disabled' END || ' name="' || l_column_name || '_font_wght" min="' || C.wght_start || '" max="' || C.wght_end || '" step="100" value="' || NVL(C.wght,400) || '"/>' ||
                    --'</label>' ||
                    '<label for="' || l_column_name || '-font-ital"' || CASE WHEN C.ital_start IS NULL THEN ' class="visually-hidden"' END || '>Italics' ||
                        '<input type="range" id="' || l_column_name || '-font-ital"' || CASE WHEN C.ital_start IS NULL THEN ' disabled' END || ' name="' || l_column_name || '_font_ital" min="' || C.ital_start || '" max="' || C.ital_end || '" value="' || NVL(C.ital,0) || '"/>' ||
                    '</label>' ||
                '</div>' ||
            '</fieldset>';
        END LOOP;
        RETURN(l_html);
    END;

    /* 
    **  BUILD HTML INPUT ELEMENTS TO EDIT WEBSITE HEADER
    */
    PROCEDURE getHeader(pWebsiteId IN website.id%type, pEyeDropper IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_html CLOB;
        l_title_font_url VARCHAR2(500);
        l_subtitle_font_url VARCHAR2(500);
        l_title_font_family font.family%type;
        l_subtitle_font_family font.family%type;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        FOR C IN (
            SELECT w.color_primary, w.header_effect, w.title, w.subtitle
              FROM website w
             WHERE w.id=pWebsiteId
        ) 
        LOOP
            l_html:=
            '<form class="editor">' ||
                '<fieldset class="background-color flex-items" style="--flex-direction:column">' ||
                    '<legend><strong>Change Background</strong></legend>' ||
                    '<div class="flex-items" style="--flex-direction:row">' ||
                        '<label for="background-color-1">Pick Color' || 
                            '<input type="color" id="background-color-1" class="background-color" name="color_primary" value="' || C.color_primary || '">' || 
                        '</label>' ||
                    CASE WHEN pEyeDropper='true' THEN
                        '<label for="background-color-2">Pick from Screen' ||
                            '<button type="button" class="eyedropper"></button>' ||
                        '</label>'
                    END ||
                    '</div>' ||
                    '<div class="flex-items" style="--flex-direction:row">' ||
                        '<button type="button" class="button background-image">Select Image</button>' || 
                        '<button type="button" class="button delete-image">No Image</button>' || 
                    '</div>' ||
                '</fieldset>' ||
                buildFontWrapper(pWebsiteId,'website.title', C.title) || 
                buildFontWrapper(pWebsiteId,'website.subtitle',C.subtitle) ||
                '<ul class="flex-items space-between buttons">' ||
                    '<li><button type="button" class="button publish-changes" data-endpoint="publish-website/" style="--button-bg:rebeccapurple">PUBLISH</button></li>' ||
                    '<li>' ||
                        '<button type="button" class="button save-changes no-focus" style="--button-bg:rebeccapurple">SAVE</button>' ||
                        '<div role="alert"></div>' ||
                    '</li>' ||
                    '<li><button type="button" class="button cancel-changes" style="--button-bg:rebeccapurple">CANCEL</button></li>' ||
                '</ul>' ||
            '</form>';
        END LOOP;
        /* Include SVG symbols in hidden element */
        /*
        l_html:=l_html || '<svg width="0" height="0">';
        FOR C IN (SELECT id, viewbox, svg FROM icon WHERE id IN ('eyedropper','image')) LOOP
            l_html:=l_html || '<symbol id="' || C.id || '" viewBox="' || C.viewbox || '" preserveAspectRatio="">' || C.svg || '</symbol>';
        END LOOP;
        l_html:=l_html || '</svg>';
        */

        apex_json.open_object;
        apex_json.write('success', TRUE); 
        apex_json.write('html', l_html);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  UPDATE WEBSITE HEADER FIELDS
    */
    PROCEDURE updateHeader(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_bodytext CLOB:=pBodyText;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
        FOR C IN (
            SELECT color_primary, title,title_font_family,title_font_size, title_color, title_underline, title_margin, title_font_wght,title_font_wdth,title_font_opsz,title_font_slnt,title_font_ital,
                subtitle,subtitle_font_family,subtitle_font_size, subtitle_color, subtitle_underline, subtitle_margin, subtitle_font_wght,subtitle_font_wdth,subtitle_font_opsz,subtitle_font_slnt,subtitle_font_ital
              FROM JSON_TABLE(l_bodytext, '$' 
              COLUMNS(color_primary,title,title_font_family,title_font_size,title_color,title_underline,title_margin,title_font_wght,title_font_wdth,title_font_opsz,title_font_slnt,title_font_ital,
                subtitle,subtitle_font_family,subtitle_font_size,subtitle_color,subtitle_underline,subtitle_margin,subtitle_font_wght,subtitle_font_wdth,subtitle_font_opsz,subtitle_font_slnt,subtitle_font_ital
              ))
        ) LOOP
            UPDATE website
               SET color_primary=C.color_primary, title=C.title, subtitle=C.subtitle, updated_date=current_timestamp
             WHERE id=pWebsiteId;
            
            UPDATE website_font
               SET font_id=C.title_font_family,
                   font_size=C.title_font_size,
                   color=C.title_color,
                   underline=C.title_underline,
                   margin=C.title_margin,
                   wght=C.title_font_wght,
                   wdth=C.title_font_wdth,
                   opsz=C.title_font_opsz,
                   slnt=C.title_font_slnt,
                   ital=C.title_font_ital,
                   updated_date=current_timestamp
             WHERE website_id=pWebsiteId
               AND context='TITLE';

            UPDATE website_font
               SET font_id=C.subtitle_font_family,
                   font_size=C.subtitle_font_size,
                   color=C.subtitle_color,
                   underline=C.subtitle_underline,
                   margin=C.subtitle_margin,
                   wght=C.subtitle_font_wght,
                   wdth=C.subtitle_font_wdth,
                   opsz=C.subtitle_font_opsz,
                   slnt=C.subtitle_font_slnt,
                   ital=C.subtitle_font_ital,
                   updated_date=current_timestamp
             WHERE website_id=pWebsiteId
               AND context='SUBTITLE';

        END LOOP;

        apex_json.open_object;
        apex_json.write('success', TRUE);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  UPDATE WEBSITE HERO IMAGE
    */
    PROCEDURE updateHero(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_bodytext CLOB:=pBodyText;
        l_hero_asset_id website.hero_asset_id%type;
        l_img VARCHAR2(1000);
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
        SELECT hero_asset_id INTO l_hero_asset_id FROM JSON_TABLE(l_bodytext, '$' COLUMNS(hero_asset_id));

        UPDATE website SET hero_asset_id=l_hero_asset_id, updated_date=current_timestamp WHERE id=pWebsiteId;

        FOR C IN (
            SELECT cld_cloud_name, resource_type, public_id, width, height, format, breakpoints
              FROM asset
             WHERE id=l_hero_asset_id
            ) 
        LOOP
            l_img:='<img "width="' || C.width || '" height="' || C.height || '"';
            l_img:=l_img || ' sizes="50vw"';
            l_img:=l_img || ' srcset="';
            FOR C1 IN (SELECT column_value AS breakpoint FROM TABLE(apex_string.split(C.breakpoints,','))) LOOP
                l_img:=l_img || 'https://res.cloudinary.com/' || C.cld_cloud_name || '/w_' || C1.breakpoint || '/' || C.public_id || ' ' || C1.breakpoint || 'w,';
            END LOOP;
            l_img:=RTRIM(l_img,',') || '">';
        END LOOP;
        
        apex_json.open_object;
        apex_json.write('success', TRUE);
        apex_json.write('img', l_img);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    /* 
    **  DELETE HERO IMAGE
    */
    PROCEDURE deleteHero(pWebsiteId IN website.id%type, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
       UPDATE website SET hero_asset_id=NULL, updated_date=current_timestamp WHERE id=pWebsiteId;
        
        apex_json.open_object;
        apex_json.write('success', TRUE);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);
    END;



    /* 
    **  BUILD HTML INPUT ELEMENTS TO EDiT WEBSITE FOOTER
    */
    PROCEDURE getFooter(pWebsiteId IN website.id%type, pEyeDropper IN VARCHAR2, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_html LONG;
        l_footer LONG;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);

        FOR C IN (
            SELECT w.footer_background_color, w.footer_color, w.header_effect, w.contact_email, w.telephone_number, w.mobile_number, w.linkedin_name, w.instagram_name
              FROM website w
             WHERE w.id=pWebsiteId
        ) 
        LOOP
            l_html:=
            '<form>' ||
                '<fieldset class="flex-items">' ||
                    '<legend><strong>Change Footer</strong></legend>' ||
                    '<div class="flex-items" style="--flex-direction:row">' ||
                        '<label for="footer-background-color-1">Pick Background Color' || 
                            '<input type="color" id="footer-background-color-1" class="footer-background-color" name="footer_background_color" value="' || C.footer_background_color || '">' || 
                        '</label>' ||
                        CASE WHEN pEyeDropper='true' THEN
                        '<label for="footer-background-color-2">Pick from Screen' ||
                            '<button type="button" class="eyedropper"></button>' ||
                        '</label>'
                        END ||
                        '<label for="footer-color">Text Color' || 
                            '<input type="color" id="footer-color" class="footer-color" name="footer_color" value="' || C.footer_color || '">' || 
                        '</label>' ||
                    '</div>' ||
                    '<div class="flex-items" style="--flex-direction:row">' ||
                        '<div>' ||
                            '<label for="contact-email">Contact Email</label>' ||
                            '<input type="email" id="contact-email" name="contact_email" maxlength="50" value="' || C.contact_email || '">' ||
                        '</div>' ||
                        
                        '<div>' ||
                            '<label for="telephone-number">Telephone</label>' ||
                            '<input type="tel" id="telephone-number" name="telephone_number" maxlength="20" value="' || C.telephone_number || '">' ||
                        '</div>' ||

                        '<div>' ||
                            '<label for="mobile-number">Mobile</label>' ||
                            '<input type="tel" id="mobile-number" name="mobile_number" maxlength="20" value="' || C.mobile_number || '">' ||
                        '</div>' ||

                        '<div>' ||
                            '<label for="linkedin-name">LinkedIn Name</label>' ||
                            '<input type="text" id="linkedin-name" name="linkedin_name" maxlength="50" autocapitalize="none" value="' || C.linkedin_name || '">' ||
                        '</div>' ||

                        '<div>' ||
                            '<label for="instagram-name">Instagram Name</label>' ||
                            '<input type="text" id="instagram-name" name="instagram_name" maxlength="50" autocapitalize="none" value="' || C.instagram_name || '">' ||
                        '</div>' ||
                    '</div>' ||
                '</fieldset>' ||

                '<ul class="flex-items space-between buttons">' ||
                    '<li><button type="button" class="button publish-changes" data-endpoint="publish-website/" style="--button-bg:rebeccapurple">PUBLISH</button></li>' ||
                    '<li>' ||
                        '<button type="button" class="button save-changes no-focus" style="--button-bg:rebeccapurple">SAVE</button>' ||
                        '<div role="alert"></div>' ||
                    '</li>' ||
                    '<li><button type="button" class="button cancel-changes" style="--button-bg:rebeccapurple">CANCEL</button></li>' ||
                '</ul>' ||
            '</form>';

            l_footer:=
            '<a class="icon-with-text" href="mailto:' || C.contact_email || '" data-contact_email>' ||
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#email"></use></svg><span>' || C.contact_email || '</span>' ||
            '</a>' ||
            '<a class="icon-with-text" href="tel:' || C.telephone_number || '" data-telephone_number>' ||
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#phone"></use></svg><span>' || C.telephone_number || '</span>' ||
            '</a>' ||
            '<a class="icon-with-text" href="tel:' || C.mobile_number || '" data-mobile_number>' ||
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#mobile"></use></svg><span>' || C.mobile_number || '</span>' ||
            '</a>' ||
            '<a class="icon-with-text" href="https://www.linkedin.com/in/' || C.linkedin_name || '" data-linkedin_name>' ||
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#linkedin"></use></svg><span>' || CASE WHEN C.linkedin_name IS NOT NULL THEN 'Let''s Connect' END || '</span>' ||
            '</a>' ||
            '<a class="icon-with-text" href="https://www.instagram.com/' || C.instagram_name || '" data-instagram_name>' ||
                '<svg class="icon" aria-hidden="true" focusable="false"><use href="#instagram"></use></svg><span>' || CASE WHEN C.instagram_name IS NOT NULL THEN 'Connect Instagram' END || '</span>' ||
            '</a>';

        END LOOP;

        /* Include SVG symbols in hidden element */
        l_html:=l_html || '<svg width="0" height="0">';
        FOR C IN (SELECT id, viewbox, svg FROM icon WHERE id IN ('email','phone','mobile','linkedin','instagram')) LOOP
            l_html:=l_html || '<symbol id="' || C.id || '" viewBox="' || C.viewbox || '" preserveAspectRatio="">' || C.svg || '</symbol>';
        END LOOP;
        l_html:=l_html || '</svg>';

        apex_json.open_object;
        apex_json.write('success', TRUE); 
        apex_json.write('html', l_html);
        apex_json.write('footer', l_footer);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

    PROCEDURE updateFooter(pWebsiteId IN website.id%type, pBodyText IN CLOB, pStatus OUT NUMBER) IS
        l_session_data pck_sec.t_session_data;
        l_bodytext CLOB:=pBodyText;
    BEGIN
        l_session_data:=pck_sec.getSessionData(pWebsiteId);
        
        FOR C IN (
            SELECT footer_background_color, footer_color, contact_email, telephone_number, mobile_number, linkedin_name, instagram_name
              FROM JSON_TABLE(l_bodytext, '$' 
              COLUMNS(footer_background_color, footer_color, contact_email, telephone_number, mobile_number, linkedin_name, instagram_name))
        ) LOOP
            UPDATE website
               SET footer_background_color=C.footer_background_color, 
                    footer_color=C.footer_color, 
                    contact_email=C.contact_email, 
                    telephone_number=C.telephone_number,
                    mobile_number=C.mobile_number, 
                    linkedin_name=C.linkedin_name, 
                    instagram_name=C.instagram_name,
                    updated_date=current_timestamp
             WHERE id=pWebsiteId;
        END LOOP;

        apex_json.open_object;
        apex_json.write('success', TRUE);
        apex_json.close_object;

        pStatus:=200;

    EXCEPTION WHEN OTHERS THEN
        pck_core.log_error(pStatus);

    END;

end;
/